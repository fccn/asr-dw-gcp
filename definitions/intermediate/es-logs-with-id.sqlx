config {
  type: "table",
  name: "es-logs-with-id",
  description: "A table containing the filtered source data with transformed components.",
  bigquery: {
    partitionBy: "full_date",
    clusterBy: ["auth_status", "institution_id", "radius_id", "nas_id"]
  }
}

WITH transformed_source AS (
  SELECT
    src.full_date,
    src.domain,
    src.radius,
    src.nas,
    src.user_hash,
    src.auth_status,
    institution.institution_id AS transformed_domain,
    radius.radius_id AS transformed_radius,
    nas.nas_id AS transformed_nas
  FROM
    ${ref("fetched-es-logs")} src
  LEFT JOIN
    ${ref("es-institutions")} institution ON src.domain = institution.domain AND src.auth_status = institution.auth_status
  LEFT JOIN
    ${ref("es-radius")} radius ON src.radius = radius.radius
  LEFT JOIN
    ${ref("es-nas")} nas ON src.nas = nas.nas
)


SELECT
  full_date,
  transformed_domain AS institution_id,
  transformed_radius AS radius_id,
  transformed_nas AS nas_id,
  user_hash,
  auth_status
FROM
  transformed_source