config {
    type: "view",
    name: "webhook-institutions",
    description: "A view containing the institutions extracted from webhook."
}

WITH deduplicated AS (
    SELECT DISTINCT
        SPLIT(CI1_codename, ':')[OFFSET(ARRAY_LENGTH(SPLIT(CI1_codename, ':')) - 1)] AS codename,
        CI1_descricao AS description,
        CI1_org AS org,
        Radius_Realms as realm,
        CI1_membro as member
    FROM ${ref("webhook_eduroam_institutions")}
),
ranked AS (
    SELECT
        codename,
        CASE 
            WHEN org IS NULL THEN description
            ELSE org
        END AS description,
        realm,
        member,
        ROW_NUMBER() OVER (PARTITION BY realm ORDER BY codename) AS row_num
    FROM deduplicated
)
SELECT
    codename,
    description,
    realm,
    member
FROM ranked
WHERE row_num = 1
